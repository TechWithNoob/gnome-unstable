# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=geocode-glib
pkgver=3.26.3+r7+gc670fb9
pkgrel=1
pkgdesc="Helper library for geocoding services"
url="https://gitlab.gnome.org/GNOME/geocode-glib"
arch=(x86_64)
license=(LGPL)
depends=(glib2 json-glib libsoup3)
makedepends=(gobject-introspection git gtk-doc meson)
provides=(libgeocode-glib-2.so)
options=(debug)
#_commit=11db75f37b77faa900255cc4835f4ab390e323bc  # tags/3.26.3^0
source=("git+https://gitlab.gnome.org/GNOME/geocode-glib.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-installed-tests=false -D soup2=false
  meson compile -C build
}

#check() {
#  meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "$pkgdir"
}
